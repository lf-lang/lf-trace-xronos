target C {
    tracing: true, // Turn on tracing.
    
    // Option 1: Plugin installed to system path (cleanest)
    trace-plugin: {
        package: "lf-trace-xronos",
        library: "lf::trace-impl",
    },

    // Option 2: Plugin installed to a user-specified path
    // trace-plugin: {
    //     package: "lf-trace-xronos",
    //     library: "lf::trace-impl",
    //     path: "path/to/install/folder", // find_package() searches here.
    // },

    // Option 3: Fully manual (dev friendly)
    // cmake-include: ["../../plugin.cmake"], // Relative or absolute path to plugin.cmake.
    // cmake-args: {
    //     LF_TRACE_PLUGIN: "lf-trace-xronos",
    //     LF_TRACE_INSTALL: "../../../install",
    // }
}

reactor Sensor {
    output out:int
    timer t(0, 1s)
    state s:int = 0
    reaction(t) -> out {=
        lf_sleep(MSEC(100));
        lf_set(out, self->s++);    
    =}
}

reactor Processing {
    input in:int
    output out:int
    reaction(in) -> out {=
        lf_sleep(MSEC(200));
        lf_set(out, in->value * -2);
    =}
}

reactor Actuator {
    input in:int 
    reaction(in) {=
        lf_sleep(MSEC(300));
        printf("Received %d\n", in->value);
    =}
}

main reactor {
    s = new Sensor()
    p = new Processing()
    a = new Actuator()
    s.out -> p.in
    p.out -> a.in
}